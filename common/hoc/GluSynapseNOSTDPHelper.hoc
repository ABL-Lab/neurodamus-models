/**
 * @file GluSynapseHelper.hoc
 * @brief Encapsulate any hoc actions that are necessary to instantiate this synapse type
 * @author king, chindemi
 * @date 04/05/2016
 * @remark Copyright Â© BBP/EPFL 2005-2016; All rights reserved. Do not distribute without further notice.
 */

/**
 * This helper object will encapsulate the hoc actions needed to create our plastic excitatory synapse
 */
begintemplate GluSynapseHelper

objref synapse, rng0, rng1
public synapse

/**
 * @param tgid gid of Postsynaptic cell
 * @param synParameters set of paramters loaded from default nrn.h5
 * @param x location of the synapse along the section (0<=x<=1)
 * @param synapseID id assigned to this synapses within the cell's context
 * @param baseSeed for random number generator
 */
proc init() { local tgid, x, synapseID, baseSeed  localobj synParams
    tgid = $1
    synParams = $o2
    x = $3
    synapseID = $4
    baseSeed = $5
    
    synapse = new GluSynapse_v3p3(x)
    synapse.tau_d_AMPA = synParams.DTC
    synapse.Use = abs( synParams.U )
    synapse.Dep = abs( synParams.D )
    synapse.Fac = abs( synParams.F )
    
    rng0 = new Random()
    rng0.MCellRan4( synapseID*100000+100, tgid+250+baseSeed )
    rng0.uniform(0,1)

    // RNG for Grapner model
    rng1 = new Random()
    rng1.MCellRan4( synapseID*100000+101, tgid+250+baseSeed )  // TODO Ask Jim about stream selection
    rng1.normal(0,1)

    synapse.setRNG( rng0, rng1 )
    synapse.synapseID = synapseID
    
    synapse.gmax_AMPA = synParams.weight
    
    // Override weight from nrn.h5
    synParams.weight = 1  // TODO Check with Jim
}

endtemplate GluSynapseHelper
